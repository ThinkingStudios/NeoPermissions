plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

group = "${project.maven_group}"
version = "${project.mod_version}+${project.minecraft_version}"
base.archivesName = "${project.archives_base_name}"

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
    maven { url "https://dl.cloudsmith.io/public/thinkingstudio/forgifiedfabricapi/maven/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        mappings "dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_patch}"
    }
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"
    // ForgifiedFabricAPI
    for (String module in ffapi_modules.split(",")) {
        // Fabric API Base
        if (module == "fabric-api-base") {
            modApi(include("org.sinytra.forgified-fabric-api:$module:0.4.42+d1308ded19")) { transitive = false }
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand "version": project.version
    }

    filteringCharset = 'UTF-8'
}

java {
    withSourcesJar()
    //withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "org.thinkingstudio"
            artifactId = project.name.toLowerCase()
            version = "${project.mod_version}+${project.minecraft_version}"

            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/thinkingstudios/NeoPermissions")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
